VAR character_class = ""
VAR scene_image = "familyLife"
LIST present_characters = You, Mum, Dad
VAR speaking_character = "none"

=== Prologue ===

Before the smoke. Before the sirens.

Warm light spills through a cracked window, painting streaks of gold across the metal walls of the small apartment on Ceres. The hum of the vibrant life of the asteroid city is distant, as though the world is holding its breath just for this moment.

Your father sits cross-legged on the floor, tools scattered around him, coaxing life back into a battered old drone. The smell of lubricants and lightly burned insulation mingles with the spices of cooking. Sparks fly as he mutters curses under his breath, but there's a grin tucked beneath his frustration.

Your mother stands nearby, stirring something rich-scented in a dented pot, humming a tune half-remembered from somewhere softer, somewhere greener.

You're here. You're safe. You're home.

* [Dad, it's not supposed to smoke like that, is it?]
	He snorts, waving a hand dismissively as another spark flies.
	"That's just… character. Drones need personality."

* [Mum, what are you making? It smells weird.]
	She laughs, flicking a bit of sauce at you with the spoon.
	"Weird? This is an old recipe I grew up eating. It's your grandmother's, probably. Or maybe I made it up. Either way, you're eating it."

* [Sit quietly, just watching them.]
	Their voices blend into a soft melody of comfort. You don't need to speak. This is enough.

- Your father finally gets the drone to hover, wobbly and sputtering like an old man complaining about bad knees.

"See? Perfect."

It promptly crashes into the table.

Your mother snorts, trying to hide her laugh behind her hand. Even you can't help it.

He grins, shaking his head.

"It's not about getting things right the first time. It's about fixing them when they break."

He ruffles your hair, smudging a bit of grease on your forehead.

"And sometimes," your mother adds, leaning down to kiss your temple, "it's about knowing when to let it go and start fresh."

You'll remember that later.

When starting fresh feels impossible.

* [I'm going to fix things better than you when I grow up.]
	Your father laughs.

 "Good. Someone's got to."

* [I'm never leaving here. It's perfect.]
	Your mother smiles softly, pulling you into a quick hug.

 "I hope you do one day. When you're ready. And when you do, take this with you."

 She taps your chest gently. "Home isn't walls. It's people."

* [Stay quiet, soaking in the warmth.]
	Your parents don't mind. They fill the space with gentle bickering and laughter, and you let it wash over you.

- The lights flicker once; just a hiccup in the power grid. Nothing unusual.

Your father glances up, frowning slightly, but he shrugs it off.

"Old station wiring. Nothing to worry about."

Your mother sets the table.

"Come eat before it gets cold."

You sit. You eat. You laugh.

And then the world ends.

-> TheEnd

=== TheEnd ===

~ scene_image = "black"

Darkness.

Not the comforting kind. Not the absence of light, but the kind that presses against your chest, heavy, suffocating.

Aharon: "Wake up."

Your eyes snap open. The world is red and gold, fire reflecting off metal walls. Sirens wail in the distance, their shrill cries cutting through the chaos. The sharp scent of burning plastic stings your nose.

You're young. Too young for this. Apparently, the world disagrees with a moral person's assessment.

Your mother's hand grips your shoulder, fingers digging in hard enough to hurt. Her face is smeared with ash, fear etched deep into lines that weren't there yesterday.

"We have to move."

* ["What's happening?"]
	Your voice is small, fragile. She doesn't answer. She just pulls you harder.

* [Try to stand on your own.]
	Your legs tremble, weak from sleep and fear. She doesn't wait, dragging you to your feet.

- The narrow corridors of your home are filled with smoke and shadows. Shapes flicker past. Neighbours? Strangers? You can't tell.

Explosions rumble somewhere distant, the ground shuddering beneath your feet. The lights overhead flicker, casting everything in stuttering flashes of white and dark.

One of the ghosts at the end of the hall resolves into the form of your father, blood streaking his temple. His breath comes in ragged gasps as he grabs your mother's arm.

"They're inside. We have to—"

He doesn't finish.

Gunfire.

A sharp crack, and he jerks as if yanked by invisible strings, and collapses in a heap.

Your mother screams, pulling you behind her as figures emerge from the smoke; faceless in black armour, their visors glowing dimly. No insignias. No words, but an abundance of precision.

She runs. You stumble, dragged like dead weight, your heart hammering so loud it drowns out the rest.

Another shot.

Her grip loosens.

You fall.

* [Scream.]
	Your voice shatters, raw and useless against the overwhelming noise.

* [Crawl to her.]
	Your knees hit the ground, slick with something warm. You reach out, fingers brushing against hers. They're already cooling.

* [Stay frozen.]
	The world narrows to a pinpoint. You can't move. You can't breathe.

- A shadow falls over you. Not the soldiers. Someone else.

A figure drops into view. A man, face hidden beneath a cracked helmet, his jacket scorched and tattered. You know that jacket. Aharon. A friend of your parents.

His voice cuts through the haze: "Stay down."

He moves like gravity bends differently around him - quick, efficient, full of contained rage. Shots ring out; bodies fall.

Silence returns, heavy and painful.

Aharon kneels beside you, pulling off his helmet. His face is streaked with grime, a fresh cut that will become a scar cuts across his cheek. Kind, tired eyes lock onto yours.

"You're safe now," he says, but it sounds like a lie.

He holds out his hand.

* [Take his hand.]
	Your fingers curl around his, trembling. His grip is steady, anchoring you to something real.

* [Cling to your mother.]
	You shake your head, tears blinding you. Aharon doesn't force it. He just waits, kneeling quietly until you can't bear it anymore.

* [Let him lift you.]
	You don't resist as he lifts you easily, cradling you like something fragile.

- The world fades to black.

21 years later, you wake up in the morning. And for a moment, the world is still black.